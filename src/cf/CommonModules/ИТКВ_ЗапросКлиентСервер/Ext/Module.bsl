#Область ПрограммныйИнтерфейс

// Возвращает текст ошибки когда запрос пустой
// Возвращаемое значение:
//   Строка	- текст ошибки когда запрос пустой
Функция ТекстОшибкиПустойЗапрос() Экспорт
	
	ТекстОшибки = НСтр("ru = 'Ожидается ВЫБРАТЬ'; en = 'Expected to SELECT'");
	Возврат СтрШаблон("{(1, 1)}: %1", ТекстОшибки);
	
КонецФункции

// Возвращает информацию о ошибке в тексте запрос
//
// Параметры:
//   ТекстОшибки - Строка - Текст ошибки вида //{(1, 1)}: Ожидается ВЫБРАТЬ
//   АнализируетсяПланЗапроса - Булево - Анализируется план запроса (присутствуют маркировочные запросы)
//
// Возвращаемое значение:
//   Структура - Информация о ошибке
//   	*Текст - Строка - Текст ошибки
//   	*НомерСтроки - Число - Номер строки
//   	*НомерСтолбца - Число - Номер столбцы
Функция ИнформацияООшибке(ТекстОшибки, АнализируетсяПланЗапроса = Ложь) Экспорт
	
	// Разбирает строку ошибки запроса
	// Например: {(19, 2)}: Синтаксическая ошибка "Справочник.Товары"
	// c 18 платформы {<Неизвестный модуль>(19, 2)}: ...
	// <<?>>Справочник.Товары КАК Товары	
	
	Если ПустаяСтрока(ТекстОшибки) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Индекс = 1;
	Если ИТКВ_Строки.РазборПрочитатьСимвол(ТекстОшибки, Индекс) = "{" Тогда
		
		// Учтем что с 18 платформы текст {<Неизвестный модуль>(19, 2)}: ...
		ИТКВ_Строки.РазборПрочитатьДоСимвола(ТекстОшибки, "(", Индекс);
		
		// Разбираем позицию ошибки
		ИТКВ_Строки.РазборПропуститьНаборСимволов(ТекстОшибки, "(", Индекс);
		НомерСтроки = ИТКВ_Строки.РазборПрочитатьЦелоеЧисло(ТекстОшибки, Индекс);
		ИТКВ_Строки.РазборПропуститьНаборСимволов(ТекстОшибки, ", ", Индекс);
		НомерСтолбца = ИТКВ_Строки.РазборПрочитатьЦелоеЧисло(ТекстОшибки, Индекс);
		ИТКВ_Строки.РазборПропуститьНаборСимволов(ТекстОшибки, ")}: ", Индекс);
		Текст = ИТКВ_Строки.РазборПрочитатьДоСимвола(ТекстОшибки, "", Индекс);
		
		Если АнализируетсяПланЗапроса Тогда
			НомерСтроки = НомерСтроки - 1;
		КонецЕсли;
		
		Результат = Новый Структура;
		Результат.Вставить("Текст", Текст);
		Результат.Вставить("НомерСтроки", НомерСтроки);
		Результат.Вставить("НомерСтолбца", НомерСтолбца);
		
	Иначе
		
		Результат = ТекстОшибки;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Новое значение сложного параметра
//
// Параметры:
//   Вид - Перечисление.ИТКВ_СложныйПараметрЗапроса - Вид
//   Значение - Произвольный - Значение
//   Представление - Строка - Представление
//
// Возвращаемое значение:
//   Структура - Данные сложного параметра
//
Функция ЗначениеСложногоПараметра(Вид, Значение, Представление = "") Экспорт
	
	Если Вид = ИТКВ_Перечисления.СложныйПараметрЗапросаВыражение() Тогда
		
		Представление = ИТКВ_Строки.Сокращенно(Значение, 70);
		
	ИначеЕсли Вид = ИТКВ_Перечисления.СложныйПараметрЗапросаТаблицаЗначений() Тогда
		
		Представление = НСтр("ru = '<Таблица значений>'; en = '<Table of values>'");
		
	ИначеЕсли Вид = ИТКВ_Перечисления.СложныйПараметрЗапросаГраница() Тогда
		
		Если Значение.Вид = ИТКВ_Перечисления.ВидГраницыВключая() Тогда
			ВидПредставление = НСтр("ru = 'Включая'; en = 'Including'");
		ИначеЕсли Значение.Вид = ИТКВ_Перечисления.ВидГраницыИсключая() Тогда
			ВидПредставление = НСтр("ru = 'Исключая'; en = 'Excluding'");
		КонецЕсли;
		
		Представление = СтрШаблон("%1; %2", ВидПредставление, Значение.Значение);
		
	КонецЕсли;
	
	Возврат Новый Структура("Вид, Значение, Представление", Вид, Значение, Представление);
	
КонецФункции

// Возвращает описание границы
//
// Параметры:
//   Значение - Граница, Дата, МоментВремени - Значение
//   Вид - Перечисление.ИТКВ_ВидГраницы - Вид границы (по умолчанию: Включая)
//
// Возвращаемое значение:
//   Структура	- Описание границы
//
Функция ОписаниеГраницы(Значение, Вид = Неопределено) Экспорт
	
	Результат = Новый Структура;
	
	Если Вид = Неопределено Тогда
		Вид = ИТКВ_Перечисления.ВидГраницыВключая();
	КонецЕсли;
	
	Возврат Новый Структура("Вид, Значение", Вид, Значение);
	
КонецФункции

// Корректирует табуляцию вставляемого текста
// Параметры:
//   ВставляемыйТекст - Строка - Вставляемый текст
//   ИсходныйТекст - Строка - Исходный текст
// Возвращаемое значение:
//   Строка - Скорректированный текст
Функция СкорректироватьТабуляциюВставляемогоТекста(ВставляемыйТекст, ИсходныйТекст) Экспорт
	
	// Определим уровень табуляции исходного текста
	ИсходныйТекстСтроки = СтрРазделить(ИсходныйТекст, Символы.ПС);
	КоличествоТабуляций = 0;
	
	Если ИсходныйТекстСтроки.Количество() >= 2 Тогда
		
		АнализируемаяСтрока = ИсходныйТекстСтроки[1];
		Для НомерСимвола = 1 По СтрДлина(АнализируемаяСтрока) Цикл
			
			Если Сред(АнализируемаяСтрока, НомерСимвола, 1) <> Символы.Таб Тогда
				Прервать;
			КонецЕсли;
			
			КоличествоТабуляций = КоличествоТабуляций + 1;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ИТКВ_Строки.ДобавитьТабВМногострочныйТекст(ВставляемыйТекст, Ложь, КоличествоТабуляций - 1);
	
КонецФункции

Функция ЗаменитьПараметрВТексте(Текст, Имя, Замена, ГраницыВыделения = Неопределено) Экспорт
	
	Результат = Текст;
	
	СтрокаПоиска = "&" + Имя;
	Индекс = СтрНайти(Текст, СтрокаПоиска);
	Если ЗначениеЗаполнено(Индекс) Тогда
		
		ИндексСледующегоСимвола = Индекс + СтрДлина(СтрокаПоиска);
		СледующийСимвол = Сред(Текст, ИндексСледующегоСимвола, 1);
		
		НаборСимволовКонецПараметра = " )," + Символы.Таб + Символы.ПС + Символы.ВК;
		СледующийСимволКонецПараметра = СтрНайти(НаборСимволовКонецПараметра, СледующийСимвол) ИЛИ СледующийСимвол = "";
		
		Если СледующийСимволКонецПараметра Тогда
			
			// Вставляем замену
			БлокПараметр = Лев(Текст, Индекс - 1) + Замена;
			Если ГраницыВыделения = Неопределено Тогда
				
				НачалоГраницы = ИТКВ_РедакторКодаКлиентСервер.КурсорПоНомеруСимвола(БлокПараметр, Индекс);
				КонецГраницы = ИТКВ_РедакторКодаКлиентСервер.КурсорПоНомеруСимвола(БлокПараметр, Индекс + СтрДлина(Замена));
				
				ГраницыВыделения = Новый Структура;
				ГраницыВыделения.Вставить("НачалоСтроки", НачалоГраницы.НомерСтроки);
				ГраницыВыделения.Вставить("НачалоКолонки", НачалоГраницы.НомерКолонки);
				ГраницыВыделения.Вставить("КонецСтроки", КонецГраницы.НомерСтроки);
				ГраницыВыделения.Вставить("КонецКолонки", КонецГраницы.НомерКолонки);
				
			КонецЕсли;
			
		Иначе
			
			БлокПараметр = Лев(Текст, ИндексСледующегоСимвола - 1);
			
		КонецЕсли;
		
		Результат = БлокПараметр + ЗаменитьПараметрВТексте(Сред(Текст, ИндексСледующегоСимвола), Имя, Замена, ГраницыВыделения);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

