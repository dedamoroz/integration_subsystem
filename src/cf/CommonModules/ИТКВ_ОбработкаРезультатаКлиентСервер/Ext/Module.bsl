#Область ПрограммныйИнтерфейс

Функция НомерСтрокиКодПеред() Экспорт
	
	Возврат -1;
	
КонецФункции

Функция НомерСтрокиКодПосле() Экспорт
	
	Возврат 9999999999;
	
КонецФункции

Функция ПредставлениеИндексаОбъекта(Индекс) Экспорт
	
	Если Индекс = НомерСтрокиКодПеред() Тогда
		
		Результат = НСтр("ru = 'Инициализация'; en = 'Initialization'");
		
	ИначеЕсли Индекс = НомерСтрокиКодПосле() Тогда
		
		Результат = НСтр("ru = 'Завершение'; en = 'Completion'");
		
	Иначе
		
		Результат = СтрШаблон("№%1", Индекс + 1);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЭтоИндексОбъекта(Индекс) Экспорт
	
	Возврат (Индекс <> НомерСтрокиКодПеред() И Индекс <> НомерСтрокиКодПосле());
	
КонецФункции

Функция ИдентификаторДополнительныеСвойства(Язык = "ru") Экспорт
	
	Возврат НСтр("ru = 'ДополнительныеСвойства'; en = 'AdditionalProperties'", Язык);
	
КонецФункции

Функция ИдентификаторСтроки(Язык = "ru") Экспорт
	
	Возврат НСтр("ru = 'Строка'; en = 'Row'", Язык);
	
КонецФункции

Функция ИдентификаторПараметры(Язык = "ru") Экспорт
	
	Возврат НСтр("ru = 'Параметры'; en = 'Parameters'", Язык);
	
КонецФункции

Функция ИдентификаторОбрабатываемыхДанных(Язык = "ru") Экспорт
	
	Возврат НСтр("ru = 'ОбрабатываемыеДанные'; en = 'ProcessedData'", Язык);
	
КонецФункции

Функция ПодсказкаПоКодуОбработки(Имя) Экспорт
	
	СтрокиПодсказки = Новый Массив;
	Если Имя = "" Тогда
		
		ШаблонПодсказки = НСтр("ru = '%1.<Имя> - Обращение к полю строки по имени';
								|en = '%1.<Name> - Access to the line field by name'");
		СтрокиПодсказки.Добавить(СтрШаблон(ШаблонПодсказки, ИдентификаторСтроки()));
		
	КонецЕсли;
	
	ШаблонПодсказки = НСтр("ru = '%1 (Структура) - Передача значений';
							|en = '%1 (Structure) - Passing values'");
	СтрокиПодсказки.Добавить(СтрШаблон(ШаблонПодсказки, ИдентификаторДополнительныеСвойства()));
	
	ШаблонПодсказки = НСтр("ru = '%1.<Имя> - Использование значений параметров запроса (только чтение)';
							|en = '%1.<Name> - Using query parameter values (read only)'");
	СтрокиПодсказки.Добавить(СтрШаблон(ШаблонПодсказки, ИдентификаторПараметры()));
	
	Если Имя = "Перед" Тогда
		
		ШаблонПодсказки = НСтр("ru = '%1 (ТаблицаЗначений) - Обрабатываемые данные (запрещено изменение)';
								|en = '%1 - (ValueTable) - Processed data (change is prohibited)'");
		СтрокиПодсказки.Добавить(СтрШаблон(ШаблонПодсказки, ИдентификаторОбрабатываемыхДанных()));
		
	КонецЕсли;
	
	Возврат СтрСоединить(СтрокиПодсказки, Символы.ПС);

КонецФункции

#КонецОбласти
